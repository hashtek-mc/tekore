plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'fr.hashtek.spigot'
version = 'R0.1.0-SNAPSHOT'

def hashrc = '/home/shuvly/Hashtek/hashrc'
def libsFolder = 'libs'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

configurations {
    implementation.canBeResolved = true
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'

    // Redis stuff
    implementation 'org.slf4j:slf4j-log4j12:2.0.13'
    implementation group: 'org.redisson', name: 'redisson', version: '3.37.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.18.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.18.0'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.18.0'

    implementation fileTree(dir: libsFolder, include: ['*.jar'])
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [
        name: rootProject.name,
        version: version
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching ('bungee.yml') {
        expand props
    }
}

javadoc {
    options.tags = [ "apiNote" ]
}

shadowJar {
    exclude '**/BungeeCord.jar'

    dependencies {
        exclude(dependency('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'))
    }

    archiveClassifier.set('')

    mergeServiceFiles()
    minimize() {
        exclude(dependency('org.redisson:redisson'))
    }
}

tasks.register('make', Copy) {
    from ('build/libs/')
    into hashrc
    include '*.jar'
}

tasks.make.dependsOn shadowJar
